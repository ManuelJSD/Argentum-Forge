plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'com.diffplug.spotless' version '8.2.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// dependencyLocking {
//     lockAllConfigurations()
// }

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

group = 'org.argentumforge'
version = '1.0.0-beta4'

import org.gradle.internal.os.OperatingSystem

project.ext.lwjglVersion = "3.4.0"
project.ext.jomlVersion = "1.10.8"
project.ext.imguiVersion = "1.90.0"
project.ext.loggerVersion = "2.7.0"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        project.ext.imguiNatives = "imgui-java-natives-linux"

        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if  (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if  (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        project.ext.imguiNatives = "imgui-java-natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"

        project.ext.imguiNatives = "imgui-java-natives-windows"
        break
}

repositories {
    mavenCentral()
}

dependencies {
    //Logger
    implementation "org.tinylog:tinylog-api:$loggerVersion"
    implementation "org.tinylog:tinylog-impl:$loggerVersion"

    // ImGUI dependencies
    implementation "io.github.spair:imgui-java-app:$imguiVersion"
    implementation "io.github.spair:imgui-java-binding:$imguiVersion"
    implementation "io.github.spair:imgui-java-lwjgl3:$imguiVersion"

    implementation "io.github.spair:$imguiNatives:$imguiVersion"

    // LWJGL3 Dependencies
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-bgfx"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-nanovg"
    implementation "org.lwjgl:lwjgl-nuklear"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-par"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-vulkan"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-bgfx::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nanovg::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nuklear::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-par::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-tinyfd"
    runtimeOnly "org.lwjgl:lwjgl-tinyfd::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'javazoom:jlayer:1.0.1'
    implementation 'com.googlecode.soundlibs:mp3spi:1.9.5.4'
    if (lwjglNatives == "natives-macos" || lwjglNatives == "natives-macos-arm64") runtimeOnly "org.lwjgl:lwjgl-vulkan::$lwjglNatives"

    // Testing
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// test {
//     useJUnitPlatform()
//     jvmArgs '--enable-native-access=ALL-UNNAMED', '--add-modules=jdk.incubator.vector'
// }

application {
    mainClass = 'org.argentumforge.Main'
}

jar {
    manifest {
        attributes(
            'Main-Class': 'org.argentumforge.Main',
            'Implementation-Title': 'Argentum Forge',
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'Argentum Forge Team'
        )
    }
}

shadowJar {
    archiveBaseName.set('ArgentumForge')
    archiveClassifier.set('') // Esto hace que el jar final no se llame "-all.jar"
    archiveVersion.set(project.version.toString())

    // Esto es crucial para librerias que usan ServiceLoader (como quizas tinylog o algunas de audio)
    mergeServiceFiles()
}

tasks.build {
    dependsOn tasks.shadowJar
    // Temporarily exclude spotlessCheck due to Java 17+ compatibility issues
    // Run './gradlew spotlessApply' manually before committing
}

tasks.jar {
    enabled = false
}

// Fix implicit dependencies for distribution tasks
tasks.distZip {
    dependsOn tasks.shadowJar
}

tasks.distTar {
    dependsOn tasks.shadowJar
}

tasks.startScripts {
    dependsOn tasks.shadowJar
}

task packageWin(type: Exec) {
    dependsOn tasks.shadowJar
    group = "distribution"
    description = "Generates a Windows executable using jpackage"

    workingDir project.projectDir

    // Detección dinámica de jpackage basada en el entorno de ejecución de Gradle
    def javaHome = System.getProperty("java.home")
    def jpackageExecutable = OperatingSystem.current().isWindows() ? "jpackage.exe" : "jpackage"
    def jpackagePath = "${javaHome}/bin/${jpackageExecutable}"

    // Permite al usuario sobrescribir la ruta con -PjpackagePath="/ruta/a/jpackage"
    if (project.hasProperty('jpackagePath')) {
        jpackagePath = project.getProperty('jpackagePath')
    }

    def inputDir = "build/libs"
    def outputDir = "build/dist"
    def jarName = "ArgentumForge-${project.version}.jar"
    def mainClass = "org.argentumforge.Main"
    def iconPath = "resources/icon.ico"

    // Verificación de seguridad
    doFirst {
        if (!new File(jpackagePath).exists()) {
            throw new GradleException("No se encontró jpackage en: ${jpackagePath}. Por favor, asegúrate de estar usando un JDK y no solo un JRE, o especifica la ruta con -PjpackagePath")
        }
        delete outputDir
    }

    commandLine jpackagePath,
            '--type', 'app-image',
            '--dest', outputDir,
            '--name', 'ArgentumForge',
            '--input', inputDir,
            '--main-jar', jarName,
            '--main-class', mainClass,
            '--icon', iconPath,
            '--description', 'Argentum Forge Map Editor',
            '--vendor', 'Argentum Forge Team'

    doLast {
        def appFolder = "${outputDir}/ArgentumForge"
        
        println "Copiando recursos externos a ${appFolder}..."
        
        copy {
            from "resources"
            into "${appFolder}/resources"
        }
        
        copy {
            from "lang"
            into "${appFolder}/lang"
        }
        
        copy {
            from "."
            include "grh_library.json", "Triggers.json"
            into appFolder
        }
        
        println "Empaquetado completado con éxito."
    }
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        googleJavaFormat('1.22.0').aosp()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

// Temporarily disable spotlessJavaCheck due to Java 17+ compatibility issues
// Developers should run './gradlew spotlessApply' manually before committing
tasks.named('spotlessJavaCheck') {
    enabled = false
}

// Workaround for 'Type T not present' error in Gradle 8.12.1+Java 21+
// This bug prevents any task of type 'Test' from being created.
// SOLUTION: Use JavaExec to run JUnit ConsoleLauncher directly.

configurations {
    junitConsole
}

dependencies {
    // Add Console Launcher dependency
    junitConsole 'org.junit.platform:junit-platform-console-standalone:1.10.0'
}

gradle.taskGraph.whenReady {
    tasks.matching { it.name == 'test' }.configureEach {
        it.enabled = false
    }
}

tasks.named('check') {
    dependsOn = ['compileJava', 'compileTestJava', 'runTests']
}

task runTests(type: JavaExec) {
    description = 'Runs unit tests using ConsoleLauncher (bypass Gradle Test task bug)'
    group = 'verification'
    
    // Ensure code is compiled before running tests
    dependsOn compileJava, compileTestJava
    
    classpath = configurations.junitConsole
    mainClass = 'org.junit.platform.console.ConsoleLauncher'
    
    // Dependencies + Compiled Main + Compiled Tests
    def fullClasspath = sourceSets.test.runtimeClasspath.getAsPath()
    
    args = [
        '--classpath', fullClasspath,
        '--scan-classpath',
        '--reports-dir', "${buildDir}/test-results"
    ]
    
    // JVM arguments for module access
    jvmArgs = [
        '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
        '--add-opens', 'java.base/java.util=ALL-UNNAMED'
    ]
}

// Disable spotlessJava to avoid 'NoSuchMethodError' in JDK 21+ during release build
tasks.named('spotlessJava') {
    enabled = false
}